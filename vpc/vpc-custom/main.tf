resource "aws_vpc" "this" {
    cidr_block = var.vpc_cidr
    enable_dns_support = true
    enable_dns_hostnames = true
    tags = merge(
        var.tags,
        {
            Name = format("%s-%s", var.name, "vpc")
        }
    )
}

# Internet Gateway
resource "aws_internet_gateway" "this" {
    vpc_id = aws_vpc.this.id
    tags = {
    Name = "${var.name}-igw"
  }
}

#Public Subnet
resource "aws_subnet" "public" {
    count = length(var.public_subnet_cidrs)
    vpc_id = aws_vpc.this.id
    cidr_block = element(var.public_subnet_cidrs, count.index)
    availability_zone = element(var.azs, count.index)
    map_public_ip_on_launch = true
    tags = {
    Name = "${var.name}-public-${count.index + 1}"
  }
}

# Private Subnet
resource "aws_subnet" "private" {
    count = length(var.private_subnet_cidrs)
    vpc_id = aws_vpc.this.id
    cidr_block = element(var.private_subnet_cidrs, count.index)
    availability_zone = element(var.azs, count.index)
    tags = {
    Name = "${var.name}-private-${count.index + 1}"
  }
}

# NAT Gateway
# NAT Gateway is used to allow instances in private subnets to access the internet for updates, etc.
resource "aws_eip" "nat" {
    # count = length(var.public_subnet_cidrs)
    domain = "vpc"
    tags = {
        Name = "${var.name}-nat-eip"
    }
    depends_on = [aws_internet_gateway.this]
}

resource "aws_nat_gateway" "this" {
    # count = length(var.public_subnet_cidrs)
    # allocation_id = element(aws_eip.nat.*.id, count.index)
    # subnet_id = element(aws_subnet.public.*.id, count.index)
    allocation_id = aws_eip.nat.id
    subnet_id = aws_subnet.public[0].id
    depends_on = [aws_internet_gateway.this]
    tags = {
        Name = "${var.name}-nat-gateway"
    }
}

# Route Table for Public Subnet
resource "aws_route_table" "public" {
    vpc_id = aws_vpc.this.id
    tags = {
        Name = "${var.name}-public-rt"
    }
}

resource "aws_route" "public_internet_access" {
    route_table_id = aws_route_table.public.id
    destination_cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.this.id
  
}

resource "aws_route_table_association" "public" {
    count = length(var.public_subnet_cidrs)
    subnet_id = element(aws_subnet.public.*.id, count.index)
    route_table_id = aws_route_table.public.id
}


# Route Table for Private Subnet
resource "aws_route_table" "private" {
    vpc_id = aws_vpc.this.id
    tags = {
        Name = "${var.name}-private-rt"
    }
}

resource "aws_route" "private_nat_access" {
    # count = length(var.private_subnet_cidrs)
    route_table_id = aws_route_table.private.id
    destination_cidr_block = "0.0.0.0/0"
    nat_gateway_id = aws_nat_gateway.this.id
    # nat_gateway_id = element(aws_nat_gateway.this.*.id, count.index)
}

resource "aws_route_table_association" "private" {
    count = length(var.private_subnet_cidrs)
    subnet_id = element(aws_subnet.private.*.id, count.index)
    route_table_id = aws_route_table.private.id
  
}

